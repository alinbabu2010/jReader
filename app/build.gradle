plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

final String BASE_URL = "BASE_URL"

def keystorePropertiesFile = rootProject.file("secret.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    ext {
        buildNumber = 2
    }

    signingConfigs {
        create("release") {
            storeFile = file(keystoreProperties["KEYSTORE_FILE"])
            storePassword = keystoreProperties["KEYSTORE_PASSWORD"]
            keyAlias = keystoreProperties["KEY_ALIAS"]
            keyPassword = keystoreProperties["KEY_PASSWORD"]
        }
    }

    compileSdk 33

    defaultConfig {
        applicationId "com.compose.jreader"
        minSdk 27
        targetSdk 33
        versionCode buildNumber
        versionName "1.0"
        buildConfigField "String", BASE_URL, '"https://www.googleapis.com/books/v1/"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.5"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    namespace 'com.compose.jreader'

    task incrementBuildNumber {
        doLast {
            String buildFileText = buildFile.getText()
            String newBuildFileText = buildFileText.replaceFirst("buildNumber = $buildNumber", "buildNumber = ${buildNumber + 1}")
            buildFile.setText(newBuildFileText)
        }
    }

}

dependencies {

    def composeBom = platform('androidx.compose:compose-bom:2023.04.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.material:material"

    // Material Icons
    implementation 'androidx.compose.material:material-icons-extended'

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Navigation compose
    implementation 'androidx.navigation:navigation-compose:2.5.3'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:31.5.0')

    // Firebase Auth and Firestore
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    //Dagger - Hilt
    kapt 'com.google.dagger:hilt-android-compiler:2.45'
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'com.google.dagger:hilt-android:2.45'
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'

    // Coroutine Lifecycle Scopes
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    // Lifecycle
    implementation('androidx.lifecycle:lifecycle-runtime-ktx:2.6.1')

    // Coil
    implementation('io.coil-kt:coil-compose:2.3.0')

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // OkHttp
    implementation('com.squareup.okhttp3:okhttp:5.0.0-alpha.7')
    implementation('com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.7')

    // JSON Converter
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Navigation animation
    implementation "com.google.accompanist:accompanist-navigation-animation:0.23.0"

    // Constraint layout compose
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'

}